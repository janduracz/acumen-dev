model Pendulum() = 
initially
  M  =  1 ,
  m  =  1 ,
  l  =  1 ,
  g  =  9.8 ,
  p  =  (-1+0.5*sin(0.2*3.14),0,0.15+0.5*cos(0.2*3.14)) ,
  x  =  0 , x' = 0 , x'' = 0 ,
  y  =  -1 , y' = 0 , y'' = 0 ,
  alpha  =  1.2 , alpha' = 0 , alpha'' = 0 ,
  beta  =  0.5 , beta' = 0 , beta'' = 0 ,
  F_x  =  0 , F_y  =  0 ,
  strike  =  0 ,
  Pi =  3.1415926 ,
  t = 0 ,
  t' = 0 ,

  _3D = (Box center=(-1,0,0) size=(0.3,0.3,0.3) color=(1,1,1) rotation=(0,0,0),
         Cylinder center=(-1+0.5*sin(0.2*3.14),0,0.15+0.5*cos(0.2*3.14)) radius= 0.01 length=1  color=(0,1,0) rotation=(0.5,-0.2,0))  
always
  
  t'  = 1 ,
  if 3<t&&t<3.02 then 
    strike+ =400 
  elseif 6<t&&t<6.02 then
    strike+ = -400
  else
    strike+ = 0 ,
     
  x'' = (F_x - m*l*(cos(alpha)*alpha'' - (alpha')^2*sin(alpha)))/(M+m) ,
  alpha''= (x'*sin(alpha)*alpha' + g*sin(alpha) - x''*cos(alpha))/l ,
  y'' = (F_y - m*l*(cos(alpha)*alpha'' - (alpha')^2*sin(alpha)))/(M+m) ,
  beta''= (y'*sin(beta)*beta' + g*sin(beta) - y''*cos(beta))/l ,
  F_x   = 64.0348*alpha + x + 4.36673*x' +19.7313*alpha'+strike ,
  F_y   = 64.0348*beta + y + 4.36673*y' +19.7313*beta'+strike ,
  p   = (x+0.5*sin(alpha),y+0.5*sin(beta),0.15+0.5*cos(alpha)) ,
  _3D = (Box center=(x,y,0) size=(0.3,0.3,0.3) color=(1,1,1) rotation=(0,0,0),
         Cylinder center=p radius=0.01 length=1 color=(0,1,0) rotation=(3.14/2-beta,-alpha,0)) 

model Main(simulator) = 
initially
  palwaysulum = create Pendulum() 
always
  simulator.timeStep + = 0.01 ,
  simulator.endTime + =10
  
