// Day/Night Heater, If Based example
//
// Fixed so the H.A. can extract a clean H.A. with no unnecessary
// modes or resets
//
// Auhor: Adam Duracz and Kevin Atkinson

class Main(simulator)
  private
    x := 10; x' := 10;
    time := 0; time' := 1;
    heating := "on"; 
    period  := "day" 
  end
  // clock
  time' = 1;
  if period == "day" && time > 12 
    period := "night";
  end;
  if period == "night" && time > 24
    period := "day";
    time := 0;
  end;
  // heater continuous
  if heating == "on"
    x' = 10
  else
    x' = -5-x
  end;
  // heater discrete
  if period == "day"
    if heating == "off" && x < 19 
      heating := "on"
    end;
    if heating == "on" && x > 23
      heating := "off"
    end
  end;
  if period == "night"
    heating := "off"
  end;
  simulator.endTime := 48
end

// Note: In this example, nested switch statements have been
//       replaced by if statements on the top level, with separate
//       ifs for continuous and discrete assignments.